pkgname=3ds-SDL2_ttf
pkgver=2.24.0
pkgrel=1
pkgdesc="Support for TrueType (.ttf) font files with SDL2 (Nintendo 3DS port)"
arch=('any')
url="https://github.com/libsdl-org/SDL_ttf"
license=('MIT')
groups=('3ds-portlibs' '3ds-sdl2-libs')
depends=(
    '3ds-freetype'
    '3ds-SDL2'
)
makedepends=(
    '3ds-pkg-config'
    'dkp-toolchain-vars'
)
options=(!strip libtool staticlibs)
source=("https://github.com/libsdl-org/SDL_ttf/releases/download/release-$pkgver/SDL2_ttf-$pkgver.tar.gz")
sha256sums=('0b2bf1e7b6568adbdbc9bb924643f79d9dedafe061fa1ed687d1d9ac4e453bfd')

build() {
    source /opt/devkitpro/3dsvars.sh

    cd SDL2_ttf-$pkgver
    sed '/^noinst_PROGRAMS/d' -i Makefile.in
    # Linking error with cmake
    ./configure \
        --prefix="$PORTLIBS_PREFIX" \
        --host=arm-none-eabi \
        --disable-shared \
        --enable-static \
        --disable-harfbuzz
    make
}

package() {
    make -C SDL2_ttf-$pkgver DESTDIR="$pkgdir" install
}
